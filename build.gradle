plugins {
	id 'java'
}

allprojects {
	group = 'dev.outldrr'
	version = '0.0.1'

	repositories {
		mavenLocal()
		mavenCentral()
	}
}

subprojects {
	if (!project.childProjects.isEmpty() || project.name == 'maven-parent') {
		return
	}

	apply plugin: 'java-library'
	java {
		toolchain {
			languageVersion.set(JavaLanguageVersion.of(17))
		}
	}

	dependencies {
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
	}

	test {
		useJUnitPlatform()
	}

	tasks.withType(JavaCompile).tap {
		configureEach {
			options.debug = true
			options.encoding = 'UTF-8'
			options.compilerArgs << '-parameters' << '-Xlint:all' << '-Xlint:-serial' << '-XprintRounds'
		}
	}
}
